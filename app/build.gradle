import au.commbank.dependencies.Config
import au.commbank.dependencies.Libs
import au.commbank.dependencies.TestLibs

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'de.mannodermaus.android-junit5'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
}

android {
    compileSdkVersion Config.compileSdkVersion

    defaultConfig {
        applicationId Config.applicationId
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode Config.versionCode
        versionName Config.versionName
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        Properties properties = new Properties()
        if (rootProject.file("local.properties").exists()) {
            properties.load(rootProject.file("local.properties").newDataInputStream())
        }

        // Inject the Maps API key into the manifest
        manifestPlaceholders = [mapsApiKey: properties.getProperty("MAPS_API_KEY", "")]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
    }

    androidExtensions {
        features = ["parcelize"]
    }

    testOptions {
        junitPlatform {
            configurationParameter "junit.jupiter.testinstance.lifecycle.default", "per_class"
        }
    }
}

dependencies {

    implementation Libs.kotlin
    implementation Libs.Android.coreKtx
    implementation Libs.Android.appcompat
    implementation Libs.Android.material
    implementation Libs.Android.constraintLayout
    implementation Libs.Android.androidxActivity
    implementation Libs.Android.liveData
    implementation Libs.Android.coroutines
    implementation Libs.Android.swipeRefreshLayout
    implementation Libs.Android.googleMaps

    //Dagger
    implementation Libs.dagger
    kapt Libs.daggerCompiler
    implementation Libs.daggerAndroid
    implementation Libs.daggerAndroidSupport
    kapt Libs.daggerAndroidProcessor

    implementation Libs.okHttpClient
    implementation Libs.okHttpLoggingInterceptor
    implementation Libs.retrofit
    implementation Libs.retrofitJsonConverter


    testImplementation TestLibs.junit5
    testImplementation TestLibs.junit5Params
    testImplementation TestLibs.mockitoCore
    testImplementation TestLibs.mockitoInline
    testImplementation TestLibs.coroutine
    testImplementation TestLibs.Android.arch
    testRuntimeOnly TestLibs.junit5Engine
}